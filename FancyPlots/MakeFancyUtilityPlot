#! /usr/bin/gnuplot

#going to do two charts, based on graemes, suggestion
#Feasability vs error
#Some of these points I can put on.
#Usefulness vs Error
#I can also put some of these points on

set terminal postscript eps enhanced color font 'Helvetica,18'
set output 'UtilityVsError.eps'

#current level = 18.05, 0.2 # General area of cancer, communicate surgical plan
#about 5 mm you could start making more informed decisions about margins
#10.00 0.4 prostate / bladder anatomy, size of central lobe.
#5.00 0.6 Size of margins
#3.00 0.8 rectal warning
#1.00 0.9 deliniate NVB and cutting planes.

f(x)=a*x**3 + b*x**2 + c * x + d 
#third column is the weights, used here to make sure the function 
#passes through 18.05,1.0 and 0.0 0.0
fit f(x) '-' using 1:2:3 via a,b,c,d 
18.05 0.2 1
10.00 0.4 5
5.00 0.6 5
3.00 0.8 5
1.00 0.9 5
0.00 1.0 1
e
#by fitting it before adding condition below I 
#can fit to points above 18.05, making it smoother
#f(x)=(x>18.05) ? 1.0 : a*x**3 + b*x**2 + c * x + d 
set title 'Clinical Utility vs Back Projected Error (mm)'
set xlabel 'Back Projected Error (mm)'
set ylabel 'Clinical Utility'
set xrange [0:20.0]
set yrange [0:1.13]
set xtics nomirror
set ytics nomirror
set border 3
#set arrow from 18.05,0.1 to 18.05,1.0 nohead
#set label 1 'Current Performance' at 18.05,1.0 left
#set label 2 'Use Fiducial Markers' at 15.15,0.75 rotate left
#set label 3 'Improve Tracking (Bias Correction)' at 9.74,0.75 rotate left
#set label 4 'Improve Tracking (Add Constraints)' at 4.51,0.75 rotate left
#set label 5 'Unknown Future Improvement' at 2.27,0.75 rotate left

plot f(x) with lines linewidth 32 linecolor rgbcolor "#cccccc" notitle, '-' using 1:2 with points pointtype 6 pointsize 3 lc rgbcolor "black" notitle , '-' using 1:2:3 with labels notitle font 'Helvetica,16'
18.05 0.2 
10.00 0.4 
5.00 0.6 
3.00 0.8
1.00 0.9 
0.00 1.0 
e
18.05 0.1 "Current Performance\nTumour Locality"
11.00 0.5 "Prostate/Bladder\nInterface"
5.00 0.55 "Size of\nMargins"
5.00 0.8 "Avoid Rectum"
3.00 1.0 "Locate NVB/\nCutting Planes"
0.00 1.0 
e

! gv UtilityVsError.eps
